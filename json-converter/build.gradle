plugins {
    id "com.jfrog.bintray" version "1.8.1"
}
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.aevi.util'
version = "${apiVersion}"
ext.applicationName = "json-converter"

apply from: '../publish-repo.gradle'
apply from: '../java-provided.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly domainDependencies.androidMockable
    implementation domainDependencies.gson

    testImplementation domainDependencies.androidMockable
    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
    testImplementation testDependencies.assertJ
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = file("../javadocs/json-converter")
    classpath = files(sourceSets.main.compileClasspath, sourceSets.main.runtimeClasspath)
    title = "Json Converter"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

def siteUrl = 'https://github.com/Aevi-UK/json-utils'
def gitUrl = 'https://github.com/Aevi-UK/json-utils.git'

bintray {
    publications = ['apiJar']
    override = true

    pkg {
        dryRun = false
        publish = true
        repo = 'aevi-uk'
        name = 'json-converter'
        userOrg = 'aevi'
        licenses = ['Apache-2.0']
        labels = ['java', 'json', 'gson']
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/Aevi-UK/json-utils/issues'
        vcsUrl = gitUrl
        publicDownloadNumbers = true
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'JsonConverter'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'brettwold'
                        name 'Brett Cherrington'
                        email 'brett@annalytics.co.uk'
                    }
                    developer {
                        id 'andreas-borglin'
                        name 'Andreas Borglin'
                        email 'andreas@jakoon.co.uk'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

apply from: '../jar-publisher.gradle'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"
